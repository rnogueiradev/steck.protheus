#INCLUDE "RWMAKE.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "AP5MAIL.CH"
#Include "TOPCONN.CH"
#INCLUDE "RPTDEF.CH"
#INCLUDE "FWPrintSetup.ch"

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ RSTFATAE     ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função desenvolvida para realizar a impressão gráfica das  º±±
±±º          ³ Cotação de Compra Steck                                    º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Cotação de Compra                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
User Function RSTFATAE(_aCotacao,cCompST,_cFilial)
	
	Local aArea   := GetArea()
	Local aArea1  := SC1->(GetArea())
	Local aArea2  := SA2->(GetArea())
	Local aArea3  := SE4->(GetArea())
	Local aArea4  := SY1->(GetArea())
	Local aArea5  := SM0->(GetArea())
	//Local   n     := 0
	
	Private cPerg 		:= "Cotação de Compra "
	Private cTime        := Time()
	Private cHora        := SUBSTR(cTime, 1, 2)
	Private cMinutos    	:= SUBSTR(cTime, 4, 2)
	Private cSegundos   	:= SUBSTR(cTime, 7, 2)
	Private cAliasLif   	:= cPerg+cHora+cMinutos+cSegundos
	Private cAlias1      := cAliasLif
	
	Private oPrint
	
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	//³ Variáveis para envio de E-Mail
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cPara         := ""
	Private cCopia        := ""
	Private cCo           := Space(200)
	Private ctitulo       := "Envio de E-Mail - "
	Private cBmpName  	 := ''
	Private cStartPath 	 := '\arquivos\cotacao_compra\'//GetSrvProfString("Startpath","") +'orcamento\'
	Private _cPath		 := GetSrvProfString("RootPath","") //GetPvProfString( GetEnvServer(), "RootPath"	, "", GetAdv97() )
	Private _cNomePdf     :=''
	Private _lRet         := .T.
	Private _cDirRel      := Getmv("MV_RELT",,'C:\ARQUIVOS_PROTHEUS\')
	Private _lxRet        := .T.
	Private _aAttach      := {}
	Private _cCaminho     := cStartPath
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variáveis do arquivo de empresas sigamat.emp
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private _cNomeCom    := ''//SM0->M0_NOMECOM
	Private _cEndereco   := ''//SM0->M0_ENDENT
	Private _cCep        := ''//SM0->M0_CEPENT
	Private _cCidade     := ''//SM0->M0_CIDENT
	Private _cEstado     := ''//SM0->M0_ESTENT
	Private _cBairro     := ''//SM0->M0_BAIRENT
	Private _cCNPJ       := ''//SM0->M0_CGC
	Private _cTelefone   := ''//SM0->M0_TEL
	Private _cFax        := ''//SM0->M0_FAX
	Private _cIe         := ''//SM0->M0_INSC
	Private _cCGC        := ''//SM0->M0_CGC
	Private cCident      := ''
	Private cCidcob      := ''
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variáveis da Cotação de Compra
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cNumCot       := cAlias1
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Variáveis do Cadastro de Comprador
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private cComprador     := ''
	Private cTelComprador  := ''
	Private cFaxComprador  := ''
	Private cMailComprador := ''
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Criação das pastas para salvar os anexos
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	If MakeDir (cStartPath) == 0
		MakeDir(cStartPath)
	Endif
	
	DbSelectArea("SY1")
	SY1->(DbGoTop())
	SY1->(DbSetOrder(1))
	If SY1->(DbSeek(xFilial("SY1")+ cCompST))
		cComprador     := SY1->Y1_NOME
		cTelComprador  := SY1->Y1_TEL
		cFaxComprador  := SY1->Y1_FAX
		cMailComprador := SY1->Y1_EMAIL
	Endif
	
	SM0->(dbSetOrder(1))
	nRecnoSM0 := SM0->(Recno())
	If SM0->(dbSeek(SUBS(cNumEmp,1,2)+_cFilial))
	 _cNomeCom    := SM0->M0_NOMECOM
	 _cEndereco   := SM0->M0_ENDENT
	 _cCep        := SM0->M0_CEPENT
	 _cCidade     := SM0->M0_CIDENT
	 _cEstado     := SM0->M0_ESTENT
	 _cBairro     := SM0->M0_BAIRENT
	 _cCNPJ       := SM0->M0_CGC
	 _cTelefone   := SM0->M0_TEL
	 _cFax        := SM0->M0_FAX
	 _cIe         := SM0->M0_INSC
	 _cCGC        := SM0->M0_CGC		
		
		//cCident := (Capital(Alltrim(SM0->M0_ENDENT))+"  "+Capital(Alltrim(SM0->M0_CIDENT))+"  - "+Alltrim(SM0->M0_ESTENT)+" - Cep: " + SUBSTR(SM0->M0_CEPENT,1,5)+"-"+SUBSTR(SM0->M0_CEPENT,6,3) )
		//cCidcob := (Capital(Alltrim(SM0->M0_ENDCOB))+"  "+Capital(Alltrim(SM0->M0_CIDCOB))+"  - "+Alltrim(SM0->M0_ESTCOB)+" - Cep: " + SUBSTR(SM0->M0_CEPCOB,1,5)+"-"+SUBSTR(SM0->M0_CEPCOB,6,3) )
	Endif

	ctitulo := ctitulo + cNumCot + " - " + _cNomeCom
	
	//Processa({|lEnd|GeraRel(_aCotacao,cCompST)})
	GeraRel(_aCotacao,cCompST)
	
	RestArea(aArea5)
	RestArea(aArea4)
	RestArea(aArea3)
	RestArea(aArea2)
	RestArea(aArea1)
	RestArea(aArea)
	
Return Nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ GeraRel      ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que realiza a geração do Relatório                  º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Pedido de Compras                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function GeraRel(_aCotacao,cCompST)
	
	Local _nK
	
	If !(_lRet)
		Aviso("Cotação de Compras"; //01 - cTitulo - Título da janela
		,"Cotação de Compras não Encontrada."+ Chr(10) + Chr(13) +;
			CHR(10)+CHR(13)+;
			"Favor Verificar.",; //02 - cMsg - Texto a ser apresentado na janela.
		{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
		,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
		,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
		,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
		,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
		,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
		,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
		,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
		)
	Else
		_cNomePdf := cEmpAnt+" - "+cNumCot
		oPrint    := fwmsprinter():New( _cNomePdf , 6      ,.F.             , cStartPath  ,.T.			,  ,  ,  ,  .f.,  ,.f.,.f. )
		oPrint    :SetLandScape()     //SetPortrait() ou SetLandscape()
		oPrint    :SetMargin(60,60,60,60)
		oPrint    :setPaperSize(9)
		
		//Processa({||ImpItens(_aCotacao,cCompST)})
		Processa( {||ImpItens(_aCotacao,cCompST) }, "Aguarde.", ,.F.)
		//ImpItens(_aCotacao,cCompST)
		
		// Indique o caminho onde será gravado o PDF
		FERASE(cStartPath+_cNomePdf+".pdf")
		oPrint:cPathPDF := cStartPath
		oPrint:Print()
		
		If ExistDir(_cDirRel)
			_lxRet := .T.
		Else
			If MakeDir(_cDirRel) == 0
				MakeDir(_cDirRel)
				Aviso("Criação de Pasta","Fora criado nesse computador uma pasta para que as Cotações de Compras emitidas lá sejam salvos após a impressão."+CHR(10)+CHR(13)+;
					CHR(10)+CHR(13)+;
					"Segue o caminho do diretório criado:"+CHR(10)+CHR(13)+;
					CHR(10)+CHR(13)+;
					_cDirRel,;
					{"OK"},3)
				_lxRet := .T.
			Else
				Aviso("Erro na Criação de Pasta","Para salvar a Cotação de Compra em questão é necessário que uma pasta seja criada nesse computador."+CHR(10)+CHR(13)+;
					CHR(10)+CHR(13)+;
					"Favor entrar em contato com o TI para que a criação da pasta seja realizada.:"+CHR(10)+CHR(13)+;
					CHR(10)+CHR(13)+;
					"Não será possível realizar a abertura da Cotação de Compra em arquivo PDF e consequentemente não será enviado via e-mail ao Fornecedor.",;
					{"OK"},3)
				_lxRet := .F.
			Endif
		Endif
		
		If _lxRet
			FERASE(_cDirRel+"\"+_cNomePdf+".pdf")
			CpyS2T(cStartPath+_cNomePdf+'.pdf',_cDirRel+"\",.T.) // COPIA ARQUIVO PARA MAQUINA DO USUÁRIO
			ShellExecute("open",_cDirRel+_cNomePdf+'.pdf', "", "", 1)
			
			If MsgBox("Envia e-mail ao Fornecedor?","Envio de E-Mail","YESNO")
				
				If ChkMail() // Abre tela Para Digitar Manualmente E-Mails
					
					Processa({|lEnd| EnvMail(oPrint,"Cotação de Compra: "+ cNumCot,{"Cotação de Compra: "+ cNumCot},cPara,cCopia,{},10)},ctitulo)
					
				Endif
				
			Else
				aadd( _aAttach  ,_cNomePdf+'.pdf')
			Endif
			
		//	For _nK := 1 To Len(_aAttach)
		//  FERASE(cStartPath+_aAttach[_nK])//Apaga anexos do Servidor
		//	Next _nK
		Endif
		
	EndIf
	
Return nil

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ ImpItens     ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que realiza a impressão dos itens e total do        º±±
±±º          ³ relatório                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Pedido de Compras                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ImpItens(_aCotacao,cCompST)
	
	Local 	oBrush
	Local _ncount
	Local _nI
	
	Private _nLin    := 4000
	Private _nLin2   := 000
	Private _nCol    := 000
	Private _nTam    := 30
	Private nColFator:= 0.5
	Private nLinFator:= 0.25
	Private nColSpace:= 3
	Private nLinSpace:= 3
	
	aBmp := "steck-new-logo.png"
	aBmp2:= "steck-cotação.png"
	
	//TFont():New( [ cName ], [ uPar2 ], [ nHeight ], [ uPar4 ], [ lBold ], [ uPar6 ], [ uPar7 ], [ uPar8 ], [ uPar9 ], [ lUnderline ], [ lItalic ] )
	oFont8  := TFont():New( "Arial"  , 0        , 8          , .T.      , .F.      , 0        , .T.      , 0        , .T.      , .F.           , .F.        )
	oFont2n := TFont():New("Times New Roman",,10,,.T.,,,,,.F. )
	oFont9  := TFont():New("Arial",9,9 ,.T.,.F.,5,.T.,5,.T.,.F.)
	oFont10 := TFont():New("Arial",9,10,.T.,.F.,5,.T.,5,.T.,.F.)
	oFont10n:= TFont():New("Arial",9,10,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont11 := TFont():New("Arial",9,11,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont12 := TFont():New("Arial",9,12,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont13 := TFont():New("Arial",9,13,.T.,.F.,5,.T.,5,.T.,.F.)
	oFont13n:= TFont():New("Arial",9,13,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont14n:= TFont():New("Arial",9,14,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont15n:= TFont():New("Arial",9,15,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont16 := TFont():New("Arial",9,16,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont16n:= TFont():New("Arial",9,16,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont17 := TFont():New("Arial",9,17,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont17n:= TFont():New("Arial",9,17,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont20 := TFont():New("Arial",9,18,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont20n:= TFont():New("Arial",9,18,.T.,.T.,5,.T.,5,.T.,.F.)
	oFont18 := TFont():New("Arial",9,18,.T.,.T.,5,.T.,5,.T.,.F.)
	
	oBrush := TBrush():New("",4)
	
	oPrint:StartPage()     // INICIALIZA a página
	ImpCabec()
	
	ProcRegua(Len(_aCotacao))
	For _nI := 1 To Len(_aCotacao)
		IncProc("Imprimindo Produto: "+_aCotacao[_nI][3])
		QuebraPagina(_nLin)
		_nCol += 010
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao dO Número da SC
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][1] ,oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_NUM")[1]+TamSX3("C1_NUM")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do Item
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][2] ,oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_ITEM")[1]+TamSX3("C1_ITEM")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do Código do Produto
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][3],oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_PRODUTO")[1]+TamSX3("C1_PRODUTO")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Descrição Produto - Campo Memo
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		_nCol2 := _nCol
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",_nTam) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do Código do Desenho
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][5],oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("B1_XDESENH")[1]+TamSX3("B1_XDESENH")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do 1º Unidade de Medida
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][6],oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( "UM" , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do 2º Unidade de Medida
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][7],oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( "2a UM" , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Quantidade na 1º Unidade de Medida
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,transform(_aCotacao[_nI][8],PesqPict("SC1","C1_QUANT") ),oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_QUANT")[1]+TamSX3("C1_QUANT")[2])) , oFont8 ))*nColFator	)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Quantidade na 2º Unidade de Medida
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,transform(_aCotacao[_nI][9],PesqPict("SC1","C1_QTSEGUM")),oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_QTSEGUM")[1]+TamSX3("C1_QTSEGUM")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do Preço Unitário
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,Replicate(_aCotacao[_nI][10],(TamSX3("C1_PRECO")[1]+TamSX3("C1_PRECO")[2])),oFont8)
		_nCol += 2+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_PRECO")[1]+TamSX3("C1_PRECO")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Alíqutota IPI
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,Replicate(_aCotacao[_nI][11],(TamSX3("C7_IPI")[1]+TamSX3("C7_IPI")[2])),oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C7_IPI")[1]+TamSX3("C7_IPI")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Data de Entrega
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][12],oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_DATPRF")[1]+TamSX3("C1_DATPRF")[2]+2)) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao do Motivo de Compra
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		oPrint:Say  (_nLin,_nCol,_aCotacao[_nI][13],oFont8)
		_nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_MOTIVO")[1]+TamSX3("C1_MOTIVO")[2])) , oFont8 ))*nColFator)
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Impressao da Descrição Produto - Campo Memo
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		_nLin2     := _nLin
		_ctexto    := Alltrim(_aCotacao[_nI][4])
		_nlinhas   := mlcount(_ctexto,_nTam)
		for _ncount:= 1 to _nlinhas
			oPrint:Say  (_nLin2 ,_nCol2, memoline(_ctexto,_nTam,_ncount),oFont8)
			If _nCount <> _nLinhas
				_nLin2 += 10
				QuebraPagina(_nLin2)
			EndIf
		next _ncount
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Finaliza a Impressão dos itens
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		
		If _nLin2 > _nLin
			_nLin := _nLin2 + 6+(oPrint:GetTextHeight("texto" , oFont8 ))*nLinFator
		Else
			_nLin += 6+(oPrint:GetTextHeight("texto" , oFont8 ))*nLinFator
			//_nLin += 10
		Endif
		
		_nCol := 000
		
		QuebraPagina(_nLin)
	Next _nI
	
	//_nLin += 10
	QuebraPagina(_nLin)
	oPrint:Line (_nLin, 005,_nLin,800) // Imprime Linha Fina
	
	cTexto  := "Só aceitaremos a Cotação de Compras se nela constar Prazo de Entrega, Condição de Pagamento, Tipo e Valor de Frete e todas as informações relativas aos Impostos."
	cTexto1 := "Por favor cotar as marcas e modelos solicitados, caso não tenha informar opcional de forma clara."
	
	_nLin += 10
	QuebraPagina(_nLin)
	oPrint:Say  (_nLin,020, "NOTA: " + cTexto						,oFont10)
	
	_nLin += 10
	QuebraPagina(_nLin)
	oPrint:Say  (_nLin,020, cTexto1						,oFont10)
	
	_nLin += 10
	QuebraPagina(_nLin)
	oPrint:Line (_nLin, 005,_nLin,800) // Imprime Linha Fina
	
	_nLin += 10
	QuebraPagina(_nLin)
	
	oPrint:EndPage()     // Finaliza a página
	
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ ImpCabec     ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que realiza a impressão do cabeçalho do relatório   º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Pedido de Compras                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ImpCabec ()
	
	_nLin := 000
	_nLin += 045
	oPrint:Box(_nLin,005,590,800)
	
	_nLin += 10
	If File(aBmp)
		oPrint:SayBitmap(_nLin+010,010,aBmp,100,030 )
	EndIf
	If File(aBmp2)
		oPrint:SayBitmap(_nLin,000,aBmp2,800,600 )
	EndIf
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Say  (_nLin,120, _cNomeCom  ,oFont10n)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Say  (_nLin,120, Capital(Alltrim(_cEndereco)) + " - " +  Capital(Alltrim(_cBairro)) ,oFont10n)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Say  (_nLin,120,"Cep: "+ SUBSTR(_cCep,1,5)+"-"+SUBSTR(_cCep,6,3) +" - "+Capital(Alltrim(_cCidade)) +" - " + (Alltrim(_cEstado)) ,oFont10n)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	If cEmpAnt == "01"
		oPrint:Say  (_nLin,120,"TEL.: (011) 2248-7000 | FAX.: (011) 2248-7051",oFont10n)
	ElseIf cEmpAnt == "03"
		oPrint:Say  (_nLin,120,"TEL.: (092) 3614-8421 | FAX.: (092) 3614-8403",oFont10n)
	Endif
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Say  (_nLin,120,"CNPJ: "  + Transform(_cCGC,PESQPICT("SA2","A2_CGC")) + " - I.E.: "+ _cIe   ,oFont10n)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Line (_nLin, 005,_nLin,800) // Imprime Linha Fina
	
	_nLin += 6+(oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Say  (_nLin,020,"Cond. Pagto" 	       ,oFont10n)
	oPrint:Say  (_nLin,075,":"                   ,oFont10n)
	oPrint:Say  (_nLin,220,"Data Emissão"			,oFont10n)
	oPrint:Say  (_nLin,290,": " + dtoc(DDataBase)	,oFont10n)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Say  (_nLin,020, "Tipo de Frete"         ,oFont10n)
	oPrint:Say  (_nLin,075, ":"                     ,oFont10n)
	oPrint:Say  (_nLin,220, "Data de Validade"      ,oFont10n)
	oPrint:Say  (_nLin,290, ": " + dtoc(Lastday(DDataBase+5,3))    ,oFont10n)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Say  (_nLin,020, "Valor de Frete"      ,oFont10n)
	oPrint:Say  (_nLin,075, ":"                   ,oFont10n)
	oPrint:Say  (_nLin,220, "Comprador"           ,oFont10n)
	oPrint:Say  (_nLin,290, ": "+Capital(Alltrim(cComprador)) + " - TEL.: " + Alltrim(cTelComprador) + " - FAX.: " + Alltrim(cFaxComprador) + " - E-MAIL.: " + Alltrim(cMailComprador) ,oFont10n)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	oPrint:Line (_nLin, 005,_nLin,800) // Imprime Linha Fina
	
	_nLin += 6+(oPrint:GetTextHeight("texto" , oFont10n ))*nLinFator
	//_nLin += 10
	nCol := 010
	oPrint:Say  (_nLin,nCol, RetTitle("C5_NUM")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_NUM")[1]+TamSX3("C1_NUM")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C7_ITEM")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_ITEM")[1]+TamSX3("C1_ITEM")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C1_PRODUTO")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_PRODUTO")[1]+TamSX3("C1_PRODUTO")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("B1_XDESEXD")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",_nTam) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("B1_XDESENH")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("B1_XDESENH")[1]+TamSX3("B1_XDESENH")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, "UM"			    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( "UM"   , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, "2a UM"		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( "2a UM" , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C1_QUANT")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_QUANT")[1]+TamSX3("C1_QUANT")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C1_QTSEGUM")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_QTSEGUM")[1]+TamSX3("C1_QTSEGUM")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C1_PRECO")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_PRECO")[1]+TamSX3("C1_PRECO")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C7_IPI")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C7_IPI")[1]+TamSX3("C7_IPI")[2])) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C7_DATPRF")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C7_DATPRF")[1]+TamSX3("C7_DATPRF")[2]+2)) , oFont8 ))*nColFator)
	
	oPrint:Say  (_nLin,nCol, RetTitle("C1_MOTIVO")		    	,oFont8)
	nCol += nColSpace+((oPrint:GetTextWidth( Replicate(" ",(TamSX3("C1_MOTIVO")[1]+TamSX3("C1_MOTIVO")[2])) , oFont8 ))*nColFator)
	
	_nLin += (oPrint:GetTextHeight("texto" , oFont8 ))*nLinFator
	//_nLin += 10
	oPrint:Line (_nLin, 005,_nLin,800) // Imprime Linha Fina
	
	_nLin += 6+(oPrint:GetTextHeight("texto" , oFont8 ))*nLinFator
	//_nLin += 10
	_nLin2 := _nLin
	
Return()

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ QuebraPagina ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que realiza a quebra de página do relatório         º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Pedido de Compras                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function QuebraPagina(_nValor)
	
	If _nValor > 580
		_nValor := 590
		_nValor += 10
		oPrint:Say  (_nValor,020, "* * *   C O N T I N U A    * * *",oFont12)
		oPrint:EndPage()     // Finaliza a página
		oPrint:StartPage()     // INICIALIZA a página
		ImpCabec()
	Endif
	
Return(_nValor)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ EnvMail      ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que realiza o envio de e-mail                       º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Pedido de Compras                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function EnvMail(oGraphic,cAssunto,aTexto,cTo,cCC,aTabela,nEspacos)
	
	Local lOk       := .F.
	Local cMensagem
	
	aTexto   := IIF( aTexto == NIL,{},aTexto )
	aTabela  := IIF( aTabela == NIL,{},aTabela)
	nEspacos := IIF( nEspacos == NIL, 10, nEspacos )
	
	cMensagem := '<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">'
	cMensagem += '<HTML><HEAD>'
	cMensagem += '<META content="text/html; charset=iso-8859-1" http-equiv=Content-Type>'
	cMensagem += '<META content="MSHTML 5.00.2920.0" name=GENERATOR></HEAD>'
	cMensagem += '<BODY aLink=#ff0000 bgColor=#ffffff link=#0000ee text=#000000 '
	cMensagem += 'vLink=#551a8b><B>'
	
	cMensagem += "<br><font Face='CALIBRI' >" + "Cotação de Compra enviada automaticamente pelo sistema PROTHEUS." + "</font></br>"
	cMensagem += "<br><font Face='CALIBRI' >" + "Enviado por: " + UsrFullName(RetCodUsr()) + "</font></br>"
	cMensagem += "<br><font Face='CALIBRI' >" + "Por favor cotar em carater de Urgência." + "</font></br>"
	cMensagem += "<br><font Face='CALIBRI' >" + "ATENÇÃO!!! AO RESPONDER ESSE E-MAIL FAVOR COPIAR A TODOS OS ENVOLVIDOS NO MESMO!!!" + "</font></br>"
	//cMensagem += "<br><font Face='CALIBRI' >" + "Cotação de Compra nº: " + cNumCot + "</font></br>"
	
	_cMesCorp:= MsgAdic()
	cMensagem += "<br><div align='left'><font Face='CALIBRI' >"+ strtran(_cMesCorp,'.',".</FONT>  </br></div>   </tr></table></br><div align='left'><font Face='CALIBRI' > ")    +'</FONT></div>'
	cMensagem := STRTRAN(cMensagem,'ZZZZ','.')
	cMensagem := STRTRAN(cMensagem,'AAAA',"</FONT></br>  </div>   </tr></table>  <div align='left'><br><font Face='CALIBRI' >")
	cMensagem += '</B>'
	cMensagem += '<BR>&nbsp;</BR>'
	
	cMensagem += "</CENTER>"
	cMensagem += '</body>'
	
	ProcRegua(8)
	
	IncProc()
	IncProc("Conectando servidor...")
	
	If  !(	U_STMAILTES(cPara,cCopia,ctitulo,cMensagem,_aAttach,_cCaminho,cCo)  )
		Aviso("E-mail não enviado"; //01 - cTitulo - Título da janela
		,"Favor verificar se os e-mails abaixo são válidos:"+ Chr(10) + Chr(13) +;
			CHR(10)+CHR(13)+;
			"E-mail Para: "+ cPara+;
			CHR(10)+CHR(13)+;
			"E-mail Cópia Oculta: "+cCo+;
			CHR(10)+CHR(13)+;
			"O E-mail não será enviado.",; //02 - cMsg - Texto a ser apresentado na janela.
		{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
		,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
		,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
		,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
		,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
		,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
		,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
		,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
		)
	EndIf
	
Return lOk

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ MsgAdic      ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que captura o texto que será utilizado para envio   º±±
±±º          ³ no corpo de e-mail                                         º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Pedido de Compras                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function MsgAdic()
	Local _cText:= ("")
	
	_cText := "Informamos que a partir de 14/03/2016 este será o novo layout das Cotações de Compras STECK."
	
Return  (alltrim(_cText))

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ ChkMail      ºAutor  ³Joao Rinaldi    º Data ³  03/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função que realiza a caixa de texto para digitação de      º±±
±±º          ³ e-mail adicional                                           º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002103 - Layout Pedido de Compras                          º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ChkMail()
	
	Local oEdit1
	Local oEdit2
	Local _nOpca     := 0
	Local _lRet	   := .F.
	Local lSaida     := .F.
	Local _nK        := 0
	Local nTamOrig   := 0
	Local nMB        := 1024
	Local nTamMax    := 6
	Private _oDlg				// Dialog Principal
	
	cPara := cMailComprador + Space(200)
	
	Do While !lSaida
		DEFINE MSDIALOG _oDlg TITLE OemtoAnsi("Envio Automático de E-Mail") FROM C(330),C(243) TO C(449),C(721) PIXEL
		
		// Cria Componentes Padrões do Sistema
		@ C(005),C(009) Say "Envia e-mail para o Fornecedor listado abaixo? (Separar por ';')" Size C(147),C(008) COLOR CLR_BLACK PIXEL OF _oDlg
		
		@ C(015),C(009) Say "Para:" Size C(147),C(008) COLOR CLR_BLACK PIXEL OF _oDlg
		@ C(015),C(036) MsGet oEdit1 Var cPara  Size C(200),C(009) COLOR CLR_BLACK PIXEL OF _oDlg Picture "@S60"
		
		@ C(030),C(009) Say "Cópia Oculta:" Size C(147),C(008) COLOR CLR_BLACK PIXEL OF _oDlg
		@ C(030),C(036) MsGet oEdit2 Var cCo Size C(200),C(009) COLOR CLR_BLACK PIXEL OF _oDlg Picture "@S60"
		
		@ C(045),C(010) Button OemtoAnsi("Envia")      Size C(037),C(012) PIXEL OF _oDlg	ACTION Eval( { || _nOpca := 1, _lRet:= .T. , _oDlg:End() }  )
		@ C(045),C(060) Button OemtoAnsi("Cancela")    Size C(037),C(012) PIXEL OF _oDlg	ACTION Eval( { || _nOpca := 2, _lRet:= .F. , _oDlg:End() }  )
		@ C(045),C(110) Button OemtoAnsi("Anexar")     Size C(037),C(012) PIXEL OF _oDlg	ACTION SaveAnexo() Of _oDlg Pixel
		@ C(045),C(160) Button OemtoAnsi("Visualizar") Size C(037),C(012) PIXEL OF _oDlg	ACTION ViewAnexo() Of _oDlg Pixel
		
		ACTIVATE MSDIALOG _oDlg CENTERED
		
		If _nOpca == 1
			For _nK := 1 To Len(_aAttach)
				nTamOrig += (Directory(cStartPath+(_aAttach[_nK]))[1,2])
			Next _nK
			
			If (nTamOrig/nMB) > (nMB*nTamMax)
				Aviso("Tamanho dos Anexos Superior ao Permitido"; //01 - cTitulo - Título da janela
				,"Os Anexos possuem tamanho superior ao tamanho máximo permitido de "+cValtoChar(nTamMax)+"MB."+ Chr(10) + Chr(13) +;
					CHR(10)+CHR(13)+;
					"Favor excluir anexos para que o e-mail possa ser enviado.",; //02 - cMsg - Texto a ser apresentado na janela.
				{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
				,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
				,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
				,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
				,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
				,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
				,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
				,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
				)
				nTamOrig := 0
				ViewAnexo()
			Else
				lSaida := .T.
				aadd( _aAttach  ,_cNomePdf+'.pdf')
			EndIf
		ElseIf _nOpca == 2
			lSaida := .T.
			aadd( _aAttach  ,_cNomePdf+'.pdf')
		Endif
	EndDo
	
	cPara := Alltrim(cPara)
	cCo   := Alltrim(cCo)
	
Return(_lRet)

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ SaveAnexo    ºAutor  ³Joao Rinaldi    º Data ³  07/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função desenvolvida para salvar anexo                      º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002612 - Automatizar Solicitação de Compras                º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function SaveAnexo()
	
	Local _cSave := ''
	Local _lRet     := .T.
	Local _cLocArq  := ''
	Local _cDir     := ''
	Local _cArq     := ''
	Local cExtensao := ''
	Local cMascara  := "Todos os arquivos"
	Local cTitulo   := "Escolha o arquivo"
	Local nMascpad  := 0
	Local cDirini   := "c:\"
	Local lSalvar   := .T. /*.F. = Salva || .T. = Abre*/
	Local lArvore   := .F. /*.T. = apresenta o árvore do servidor || .F. = não apresenta*/
	Local _cMsgSave := ""
	Local aArea1    := GetArea()
	
	_cLocArq  := cGetFile( cMascara, cTitulo, nMascpad, cDirIni, lSalvar,, lArvore)
	
	If !(Empty(_cLocArq))
		_cLocArq  := Alltrim(_cLocArq)
		_cDir     := SUBSTR(_cLocArq, 1                      ,RAT( "\"   , _cLocArq ))
		_cArq     := SUBSTR(_cLocArq, RAT( "\"   , _cLocArq ),Len(_cLocArq))
		_cArq     := StrTran(_cArq,"\","")
		cExtensao := SUBSTR(_cLocArq,Rat(".",_cLocArq),Len(_cLocArq))
		
		If Len(Directory(cStartPath+_cArq)) = 1
			Aviso("Seleção de Anexos"; //01 - cTitulo - Título da janela
			,"O Arquivo '"+_cArq+"' já foi anexado."+ Chr(10) + Chr(13)+;
				CHR(10)+CHR(13)+;
				"Favor selecionar um novo arquivo para ser anexado.",; //02 - cMsg - Texto a ser apresentado na janela.
			{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
			,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
			,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
			,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
			,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
			,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
			,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
			,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
			)
			
			Return
		Endif
		
		If At(".",cExtensao) == 1
			aadd( _aAttach  ,_cArq)
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Copio o arquivo original da máquina do usuário para o servidor
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			lSucess   := __CopyFile(_cLocArq,cStartPath+_cArq)
			
			If !(lSucess)
				Aviso("Problema ao Anexar Arquivo"; //01 - cTitulo - Título da janela
				,"O Arquivo '"+_cArq+"' não foi anexado."+ Chr(10) + Chr(13) +;
					CHR(10)+CHR(13)+;
					"Favor verificar com o TI.",; //02 - cMsg - Texto a ser apresentado na janela.
				{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
				,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
				,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
				,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
				,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
				,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
				,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
				,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
				)
			EndIf
		Else
			Aviso("Problema com Extensão do Anexo"; //01 - cTitulo - Título da janela
			,"A Extensão "+cExtensao+" é inválida para anexar junto a Solicitação de Compras."+ Chr(10) + Chr(13) +;
				CHR(10)+CHR(13)+;
				"Ação não permitida.",; //02 - cMsg - Texto a ser apresentado na janela.
			{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
			,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
			,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
			,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
			,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
			,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
			,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
			,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
			)
		Endif
	Else
		Aviso("Anexar Arquivo"; //01 - cTitulo - Título da janela
		,"Nenhum Arquivo foi selecionado para ser anexado.",; //02 - cMsg - Texto a ser apresentado na janela.
		{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
		,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
		,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
		,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
		,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
		,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
		,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
		,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
		)
	Endif
	
	RestArea(aArea1)
	
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ ViewAnexo ºAutor  ³Joao Rinaldi       º Data ³  07/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para que o comprador possa visualizar os arquivos   º±±
±±º          ³ anexados e com a opção de marcar os anexos que serão       º±±
±±º          ³ excluídos                                                  º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002612 - Automatizar Solicitação de Compras                º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function ViewAnexo()
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Declaração das Variáveis
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local _aAnexo    := {}
	Local _nJ
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Variáveis para montagem da Tela - MsNewGetDados
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local _cTitulo     := "Relação de Anexos"
	Local aSize        := MsAdvSize(, .F., 400)
	Local aInfo        := {aSize[1],aSize[2],aSize[3],aSize[4]-12, 1, 1 }
	Local aObjects     := {{100, 100,.T.,.T. }}
	Local aPosObj      := MsObjSize( aInfo, aObjects,.T. )
	Local nStyle       := GD_INSERT+GD_DELETE+GD_UPDATE
	Local _aCamposEdit := {} //{"COR","D2_FILIAL","D2_DOC"}
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Variáveis de Ação da MsNewGetDados
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local _aButtons    := {}
	Local _bOk         := {|| (STTOK(@_aAnexo,@oDlgCons))}
	Private _bCancel   := {|| oDlgCons:End() }
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Variáveis de Objeto e Flag da MsNewGetDados
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private oGetDados1
	Private oOk        := LoadBitmap( GetResources(), "LBOK" )//"MARCADO"
	Private oNo        := LoadBitmap( GetResources(), "LBNO" )//"DESMARCADO"
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Variávies do Cabeçalho e Itens da MsNewGetDados
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Private _aHeader   := {}
	//Private _aAnexo   := {}
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³Tamanho de Fonte a ser exibida na Tela
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	oFont13 := TFont():New("Arial",9,24,.T.,.F.,5,.T.,5,.T.,.F.)
	
	If Len(_aAttach) >= 1
		For _nJ := 1 to Len (_aAttach)
			aAdd(_aAnexo,{"LBNO",;
				cValtoChar(_nJ),;
				_aAttach[_nJ],;
				CValtoCHar(Round(Directory(cStartPath+(_aAttach[_nJ]))[1,2]/1024,0)) + " KB",;
				.F.})
		Next _nJ
		STaHeader()
		
		Define MSDialog oDlgCons Title _cTitulo From aSize[7],000 To aSize[6],aSize[5] of GetWndDefault() Pixel
		
		@ aPosObj[1,1]+2,aPosObj[1,2]+5 Say "Marque os Arquivos que serão excluídos:" Of oDlgCons Pixel COLOR CLR_HBLUE FONT oFont13
		oGetDados1 := MsNewGetDados():New( aPosObj[1,1]+20      , aPosObj[1,2]+5                , aPosObj[1,3]     , aPosObj[1,4] ,              ,"AllWaysTrue","AllWaysTrue",""        , _aCamposEdit         ,/*freeze*/,999,/*fieldok*/,/*superdel*/,/*delok*/,oDlgCons,_aHeader,_aAnexo)
		bDbClick := oGetDados1:oBrowse:bLDblClick
		oGetDados1:oBrowse:bldblClick := {|| STClick(@_aAnexo) }
		oGetDados1:oBrowse:SetFocus()
		
		ACTIVATE MSDIALOG oDlgCons CENTERED ON INIT EnchoiceBar(oDlgCons, _bOk, _bCancel,,_aButtons)
		
	Else
		Aviso("Seleção de Anexos"; //01 - cTitulo - Título da janela
		,"Não existe nenhum anexo a ser visualizado."+CHR(10)+CHR(13)+;
			CHR(10)+CHR(13)+;
			"",;//02 - cMsg - Texto a ser apresentado na janela.
		{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
		,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
		,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
		,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
		,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
		,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
		,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
		,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
		)
	Endif
	
Return _aAttach

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ STaHeader ºAutor  ³Joao Rinaldi       º Data ³  07/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para retornar o aHeader                             º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002612 - Automatizar Solicitação de Compras                º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function STaHeader()
	
	//aAdd(_aHeader,{Título                 ,Campo         ,Picture                      ,Tamanho                  ,Decimal                   ,Validação,Reservado        ,Tipo                    ,Reservado,Reservado})
	Aadd(_aHeader,{"Flag"                   ,"XX_FLAG"     ,"@BMP"                       ,1                        ,0                         ,""       ,""               ,""                      ,"","","","",".T."})
	aAdd(_aHeader,{RetTitle("C7_ITEM")      ,"C7_ITEM"     ,PesqPict("SC7","C7_ITEM")    ,TamSx3("C7_ITEM")[1]     ,TamSx3("C7_ITEM")[2]      ,""       ,"",TamSx3("C7_ITEM")[3]    ,"","","","",".T."})
	aAdd(_aHeader,{RetTitle("J4_NOMARQ")    ,"J4_NOMARQ"   ,PesqPict("SJ4","J4_NOMARQ")  ,TamSx3("J4_NOMARQ")[1]   ,TamSx3("J4_NOMARQ")[2]    ,""       ,"",TamSx3("J4_NOMARQ")[3]  ,"","","","",".T."})
	aAdd(_aHeader,{"Tamanho do Arquivo"     ,"C1_QUANT"    ,"@999,999,999"               ,TamSx3("C1_QUANT")[1]    ,TamSx3("C1_QUANT")[2]     ,""       ,"",TamSx3("C1_QUANT")[3]   ,"","","","",".T."})
	
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ STClick   ºAutor  ³Joao Rinaldi       º Data ³  07/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para validar o Clique acionado pelo usuário na tela º±±
±±º          ³                                                            º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002612 - Automatizar Solicitação de Compras                º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function STClick(_aAnexo,oDlgCons)
	
	Local nPosFLAG := aScan(oGetDados1:aHeader,{|x| AllTrim(x[2]) == "XX_FLAG"})
	
	If oGetDados1:aCols[oGetDados1:nAt][nPosFLAG] == "LBNO" // DESMARCADO
		oGetDados1:aCols[oGetDados1:nAt][nPosFLAG] := "LBOK" // MARCADO
		oGetDados1:oBrowse:Refresh()
	Else
		oGetDados1:aCols[oGetDados1:nAt][nPosFLAG] := "LBNO" // DESMARCADO
		oGetDados1:oBrowse:Refresh()
	Endif
	
Return

/*
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÉÍÍÍÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍËÍÍÍÍÍÍÑÍÍÍÍÍÍÍÍÍÍÍÍÍ»±±
±±ºPrograma  ³ STTOK     ºAutor  ³Joao Rinaldi       º Data ³  07/03/2016 º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÊÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºDesc.     ³ Função para validar a Confirmação das informações marcadas º±±
±±º          ³ na tela pelo usuário                                       º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºUso       ³ Steck Industria Eletrica Ltda.                             º±±
±±ÌÍÍÍÍÍÍÍÍÍÍØÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¹±±
±±ºChamado   ³ 002612 - Automatizar Solicitação de Compras                º±±
±±ºSolic.    ³ Juliana Queiroz - Depto. Compras                           º±±
±±ÈÍÍÍÍÍÍÍÍÍÍÏÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍÍ¼±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function STTOK(_aAnexo)
	
	Local nPosFLAG   := aScan(oGetDados1:aHeader,{|x| AllTrim(x[2]) == "XX_FLAG"})
	Local nPosFile   := aScan(oGetDados1:aHeader,{|x| AllTrim(x[2]) == "J4_NOMARQ"})
	Local _nK
	Local cStatus    := ''
	Local _nAnexo    := 0
	Local cTexto1    := ''
	Local cTexto2    := ''
	
	For _nK := 1 to Len(oGetDados1:aCols)
		IF oGetDados1:aCols[_nK][nPosFLAG] == "LBOK" // MARCADO
			_nAnexo ++
		EndIf
	Next _nK
	
	If (_nAnexo > 0)
		If _nAnexo == 1
			cTexto1 := "Existe apenas" + cValtoChar(_nAnexo) + " Item Selecionado."
			cTexto2 := "Deseja excluir o anexo ?"
		Else
			cTexto1 := "Existem " + cValtoChar(_nAnexo) + " Itens Selecionados."
			cTexto2 := "Deseja excluir os anexos ?"
		Endif
		
		If MsgYesNo(cTexto1+ Chr(10) + Chr(13) +cTexto2,"Seleção de Anexos")
			For _nK := 1 to Len(oGetDados1:aCols)
				IF oGetDados1:aCols[_nK][nPosFLAG] == "LBOK" // MARCADO
					FERASE(cStartPath+_aAttach[_nK])//Apaga anexos do Servidor
				EndIf
			Next _nK
			
			_aAttach := {}
			For _nK := 1 to Len(oGetDados1:aCols)
				IF !oGetDados1:aCols[_nK][nPosFLAG] == "LBOK" // MARCADO
					aadd( _aAttach  ,oGetDados1:aCols[_nK][nPosFile])
				EndIf
			Next _nK
		Endif
		
	Else
		Aviso("Seleção de Anexos"; //01 - cTitulo - Título da janela
		,"Nenhum Arquivo foi Selecionado para ser excluído."+CHR(10)+CHR(13)+;
			CHR(10)+CHR(13)+;
			"",;                          //02 - cMsg - Texto a ser apresentado na janela.
		{"OK"};                          //03 - aBotoes - Array com as opções dos botões.
		,3;                              //04 - nSize - Tamanho da janela. Pode ser 1, 2 ou 3
		,;                               //05 - cText - Titulo da Descrição (Dentro da Janela)
		,;                               //06 - nRotAutDefault - Opção padrão usada pela rotina automática
		,;                               //07 - cBitmap - Nome do bitmap a ser apresentado
		,.F.;                            //08 - lEdit - Determina se permite a edição do campo memo
		,;                               //09 - nTimer - Tempo para exibição da mensagem em segundos.
		,;                               //10 - nOpcPadrao - Opção padrão apresentada na mensagem.
		)
	Endif
	
Return oDlgCons:End()
